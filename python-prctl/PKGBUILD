# Maintainer: Vadim Kuznetsov <rics@gmx.it>

pkgname=('python-prctl' 'python2-prctl')
pkgver=1.6.1
pkgrel=2
pkgdesc='Python interface to the prctl() syscall'
arch=('i686' 'x86_64')
url='https://github.com/seveas/python-prctl'
license=('GPL3')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("https://github.com/seveas/python-prctl/archive/v${pkgver}.tar.gz")
md5sums=('81fdc761aebf7dafbeb375a55a2d165d')

prepare()
{
    cd "$srcdir"
    cp -r python-prctl-$pkgver python2-prctl-$pkgver
}

build()
{
    cd "$srcdir"/python-prctl-$pkgver
    python setup.py build
    cd ../python2-prctl-$pkgver
    python2 setup.py build
}

package_python-prctl()
{
    depends=('python' 'libcap')
    cd "$srcdir"/python-prctl-$pkgver
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-prctl()
{
    depends=('python2' 'libcap')
    cd "$srcdir"/python2-prctl-$pkgver
    python2 setup.py install --root="${pkgdir}" --optimize=1
}
