# Maintainer: Vadim Kuznetsov <vimusov@gmail.com>

pkgname=yacron
pkgver=1.0
pkgrel=2
pkgdesc='Yet another cron-like daemon'
arch=('any')
url='https://github.com/vimusov/yacron'
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("git+https://github.com/vimusov/${pkgname}.git#tag=v${pkgver}" "${pkgname}.service")
md5sums=('SKIP' '2ef4007c4e1cd75e8aa99980bb6768af')

build()
{
    cd "$srcdir"/"$pkgname"
    python setup.py build
}

check()
{
    cd "$srcdir"/"$pkgname"
    python setup.py test
}

package()
{
    cd "$srcdir"/"$pkgname"
    python setup.py install --optimize=1 --skip-build --root="$pkgdir"
    install -D --mode=0644 \
        "$srcdir"/"${pkgname}.service" \
        "$pkgdir"/usr/lib/systemd/system/"${pkgname}.service"
}
