pkgbase=gpicview
pkgname="${pkgbase}-ng"
pkgver=0.2.5
pkgrel=1
pkgdesc='Lightweight image viewer'
arch=('x86_64')
license=('GPL2')
url='https://lxde.org/'
depends=('gtk3')
makedepends=('intltool')
conflicts=('gpicview' 'gpicview-gtk3')
source=(https://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz
        0001-Fix-displaying-images-with-GTK3.patch
        0002-gif-animation-fixup.patch)
sha256sums=('38466058e53702450e5899193c4b264339959b563dd5cd81f6f690de32d82942'
            'f597fa7d5e8537665ea7bdf7bfffebaa32046e8feb4866866bfb64c219d8ea6d'
            'dc5059620327b87aa4fea116a758eaac8a9a6834c533fb3759fb24b779627b6a')

prepare()
{
    cd "$srcdir"/"${pkgbase}-$pkgver"

    # Fix displaying images with GTK3
    # https://sourceforge.net/p/lxde/patches/542/
    patch -Np1 -i ../0001-Fix-displaying-images-with-GTK3.patch

    patch -p0 -i ../0002-gif-animation-fixup.patch

    sed -i '/^Categories=/ s/Utility;//' gpicview.desktop.in
}

build()
{
    cd "$srcdir"/"${pkgbase}-$pkgver"
    ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3 --disable-nls
    make
}

package()
{
    make -C "$srcdir"/"${pkgbase}-$pkgver" DESTDIR="$pkgdir" install
}
