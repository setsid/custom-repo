# Maintainer: Vadim Kuznetsov <rics@gmx.it>

pkgname=('python-pony' 'python2-pony')
pkgver=0.7.1
pkgrel=1
pkgdesc='Pony Object-Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
license=('AGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/ponyorm/pony/archive/${pkgver}.tar.gz")
md5sums=('1281514a9f206f0d9ef59488d3e507f2')

prepare()
{
    cd "$srcdir"
    cp -ax "pony-$pkgver" pony-py2
    find pony-py2 -name '*.py' -exec sed -e 's_#!/usr/bin/env python_&2_' -i {} \;
}

build()
{
    cd "$srcdir"/"pony-$pkgver"
    python setup.py build
    cd ../pony-py2
    python2 setup.py build
}

package_python-pony()
{
    depends=('python')
    cd "$srcdir"/"pony-$pkgver"
    python setup.py install --skip-build --root="$pkgdir" --optimize=1
    rm -rf "$pkgdir"/usr/lib/python3.6/site-packages/pony/orm/{examples,tests}
}

package_python2-pony()
{
    depends=('python2')
    cd "$srcdir"/pony-py2
    python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
    rm -rf "$pkgdir"/usr/lib/python2.7/site-packages/pony/orm/{examples,tests}
}
