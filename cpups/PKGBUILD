# Maintainer: Vadim Kuznetsov <vimusov@gmail.com>

pkgname=cpups
pkgver=1.3.2
pkgrel=1
pkgdesc='CyberPower UPS monitoring and management software'
arch=('x86_64')
url='https://www.cyberpowersystems.com/product/software/powerpanel-for-linux/'
license=('custom')
depends=('libusb')
makedepends=('patch')
backup=("etc/pwrstatd.conf")
options=('!strip')
source=(
    "${pkgname}-$pkgver.tgz::https://dl4jz3rbrsfum.cloudfront.net/software/powerpanel-${pkgver//./}-x86_64.tar.gz"
    'poweroff'
    'pwrstatd.conf.patch'
    'rules'
    'service'
)
md5sums=('d69233e117f92611931cb820bad141df'
         '534424123a3e9bab187cb62d83424568'
         '6bd7b531b1f4235785f31144162c6543'
         '542552fedb7d684081417c680f35a58b'
         'aecac08d02317441806702bf26523be5')

prepare()
{
    patch --strip=0 --directory="$srcdir"/"powerpanel-$pkgver"/conf --input="$srcdir"/pwrstatd.conf.patch
}

package()
{
    cd "$srcdir"/"powerpanel-$pkgver"
    install -D --mode=0644 --target-directory="$pkgdir"/etc conf/pwrstatd.conf
    install -D --mode=0755 --target-directory="$pkgdir"/usr/bin bin/pwrstat bin/pwrstatd
    install -D --mode=0755 "$srcdir"/poweroff "$pkgdir"/usr/bin/cpups-poweroff
    install -D --mode=0644 "$srcdir"/rules "$pkgdir"/etc/udev/rules.d/99-"${pkgname}.rules"
    install -D --mode=0644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/"${pkgname}.service"
}
